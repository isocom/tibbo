include "global.tbh"

dim deviceMode as Mode = In

'====================================================================

sub on_sys_init()
  beep.divider = 10000
end sub

sub on_sys_timer()
  device_tr610_init()
  device_watch_dog()

  dhcp_proc_timer()
  displayTimer()
  keypadTimer()
  tcpttTimer()
end sub

sub on_sock_data_arrival()
  dhcp_proc_data()
  dns_proc_data()
  gae_proc_data()
  rs232ProcessSocket()
end sub

sub on_ser_data_arrival()
  readerProcess
  rs232Process
  tcpttProcess
end sub

sub on_sock_inband()
  dim buf as string 'we will keep the data from the CMD buffer here
  dim cmd as string 'this will keep individual inband commands
  dim x as byte
  
  buf = sock.getinband 'we get entire CMD buffer contents into the s
  x=instr(1, buf, chr(sock.endchar), 1)
  while x<>0
    cmd = left(buf, x-1) 's2 now contains a single inband command
    buf = right(buf, len(buf)-x) 'cut out this command               
    'process inband command in the s2 as needed
	cmd_process(cmd)
    'any more inband commands to process now?                
    x=instr(1, buf, chr(sock.endchar), 1)
  wend
end sub

sub setDeviceMode(newDeviceMode as Mode, verbose as boolean)
  select case newDeviceMode
    case in:
	  if verbose then displayMessage("Rozpoczecie pracy")
      deviceMode = In
	  keypadLEDLit(kp_button_lu, true)
	  keypadLEDLit(kp_button_ld, false)
	  keypadLEDLit(kp_button_ru, false)
	  keypadLEDLit(kp_button_rd, false)
    case out:
      if verbose then displayMessage("Zakonczenie pracy")
	  deviceMode = Out
	  keypadLEDLit(kp_button_lu, false)
	  keypadLEDLit(kp_button_ld, true)
	  keypadLEDLit(kp_button_ru, false)
	  keypadLEDLit(kp_button_rd, false)
    case tempin:
	  if verbose then displayMessage("Powrot z wyjscia`sluzbowego")
	  deviceMode = TempIn
	  keypadLEDLit(kp_button_lu, false)
	  keypadLEDLit(kp_button_ld, false)
	  keypadLEDLit(kp_button_ru, true)
	  keypadLEDLit(kp_button_rd, false)
    case tempout:
	  if verbose then displayMessage("Wyjscie sluzbowe")
	  deviceMode = TempOut
	  keypadLEDLit(kp_button_lu, false)
	  keypadLEDLit(kp_button_ld, false)
	  keypadLEDLit(kp_button_ru, false)
	  keypadLEDLit(kp_button_rd, true)
  end select
end sub

sub on_kp(key_event as pl_kp_event_codes, key_code as byte)
  dim key as KeypadKeyEnum = keypadOnKey(key_event, key_code)
  
  if key_event <> PL_KP_EVENT_RELEASED then exit sub
  
  if key = KP_BUTTON_LU then setDeviceMode(In, true)
  if key = KP_BUTTON_LD then setDeviceMode(Out, true)
  if key = KP_BUTTON_RU then setDeviceMode(TempIn, true)
  if key = KP_BUTTON_RD then setDeviceMode(TempOut, true)
end sub

sub callback_new_card_detected(byref card_number as string)
  displayMessage("Odczytano karte:`" + card_number)
  gae_send_card_detected1(card_number, deviceMode)
end sub
